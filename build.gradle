buildscript {
    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
            maven { url 'https://dl.bintray.com/alphagov/maven-test' }
            jcenter()
        }
        else {
            maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
        }
    }
    dependencies {
        classpath 'com.github.spullara.mustache.java:compiler:0.8.10',
                'org.yaml:snakeyaml:1.10',
                'com.google.guava:guava:14.0.1',
                'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'

ext {
    opensaml_version = '3.3.0'
    dropwizard_version = '1.2.0'
    utils_version = '2.0.0-346'
    saml_libs_version = "${opensaml_version}-160"
    build_version = "$opensaml_version-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
}


subprojects {

    apply plugin: 'java'

    group = "uk.gov.ida"
    version = "0.1.$version"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
            maven { url 'https://dl.bintray.com/alphagov/maven' } // For dropwizard-logstash
            maven { url 'https://dl.bintray.com/alphagov/maven-test' } // For other public verify binaries
            jcenter()
        }
        else {
            maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
        }
    }

    configurations {
        common
        dropwizard
        saml
        saml_test
    }

    dependencies {
        dropwizard (
                "io.dropwizard:dropwizard-core:${dropwizard_version}",
                "io.dropwizard:dropwizard-client:${dropwizard_version}",
                "io.dropwizard:dropwizard-views-mustache:${dropwizard_version}"
        )

        saml (
                "org.opensaml:opensaml-core:${opensaml_version}",
                "org.opensaml:opensaml-saml-impl:${opensaml_version}",
                "uk.gov.ida:saml-extensions:${saml_libs_version}",
                "uk.gov.ida:saml-metadata-bindings:${saml_libs_version}",
                "uk.gov.ida:saml-security:${saml_libs_version}",
                "uk.gov.ida:hub-saml:${opensaml_version}-15682",
                "se.litsec.eidas:eidas-opensaml3:1.2.0",
                "se.litsec.opensaml:opensaml3-ext:1.1.0"
        )

        saml_test (
                "net.sourceforge.htmlunit:htmlunit:2.28",
                "uk.gov.ida:common-test-utils:${utils_version}",
                "uk.gov.ida:saml-metadata-bindings-test:${saml_libs_version}",
                "io.dropwizard:dropwizard-testing:${dropwizard_version}",
        )

        common (
                "javax.xml.bind:jaxb-api:2.3.0",
                "javax.activation:activation:1.1.1",
                "uk.gov.ida:security-utils:${utils_version}"
        )

        testCompile (
            "org.junit.jupiter:junit-jupiter-api:5.0.1",
            "org.junit.platform:junit-platform-launcher:1.0.1",
            "org.hamcrest:hamcrest-core:1.3",
            "org.hamcrest:hamcrest-library:1.3",
            "org.mockito:mockito-core:2.23.0",
            "io.dropwizard:dropwizard-testing:${dropwizard_version}",
            "net.sourceforge.htmlunit:htmlunit:2.28",
            project(':proxy-node-test')
        )

    }
}

defaultTasks 'clean', 'test'

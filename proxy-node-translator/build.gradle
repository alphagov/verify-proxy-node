repositories {
    // cloudhsm libraries downloaded from AWS
    flatDir {
        dirs '/opt/cloudhsm/java'
    }
}

dependencies {
    compile configurations.dropwizard,
            configurations.opensaml,
            configurations.verify_saml,
            configurations.common,
            configurations.ida_utils,
            project(':proxy-node-shared')

    if (project.hasProperty('cloudhsm')) {
        compile name: 'cloudhsm-2.0.0'
        compile name: 'log4j-api-2.8'
        compile name: 'log4j-core-2.8'
    }

    testCompile configurations.verify_saml_test,
            project(':proxy-node-shared').sourceSets.test.output
}

sourceSets {
    test {
        resources {
            srcDir '../.local_pki'
        }
    }
}

group = 'uk.gov.ida.notification.translator'
version = "${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

apply plugin: 'application'
mainClassName = 'uk.gov.ida.notification.translator.TranslatorApplication'
applicationDefaultJvmArgs = ["-Dfile.encoding=utf-8"]

run {
    args 'server', './src/dist/config.yml'
}
